apply plugin: 'com.android.application'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream();
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout;
        }
        String ver = stdout.toString().trim();
        if (ver.startsWith("v") && ver.charAt(1).isDigit()) {
            ver = ver.substring(1);
        }
        return ver;
    }
    catch (ignored) {
        return null;
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.woalk.apps.xposed.notifcount"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 7
        versionName getVersionName()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    provided fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':android-colorpicker')
}
